apply plugin: 'com.android.application'

def getProperty(String fileName, String propertyName) {
    def propsFile = rootProject.file(fileName)
    if(propsFile.exists()) {
        def properties = new Properties()
        properties.load(new FileInputStream(propsFile))
        if(properties[propertyName] != null) {
            return properties[propertyName]
        } else {
            print("Undefined property: "+propertyName + " inside file: "+fileName)
        }
    } else {
        print(fileName +" does not exist.")
    }
}

project.ext {
    supportLibraryVersion = "26.1.0"
    daggerVersion = "2.13"
    retrofitVersion = "2.3.0"
    butterKnifeVersion = "8.8.1"
    rxJavaVersion = "2.1.0"
    rxAndroidVersion = "2.0.1"
    lifecycleVersion = "1.0.0"
}

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "prac.retrofit.com.au.retroprac"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "API_KEY", "\"${getProperty("local.properties", "movie_api_key")}\""
        buildConfigField "String", "BASE_URL", "\"http://api.themoviedb.org/\""
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$project.supportLibraryVersion"
    implementation "com.android.support:design:$project.supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$project.supportLibraryVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // Lifecycle
    implementation "android.arch.lifecycle:runtime:$project.lifecycleVersion"
    implementation "android.arch.lifecycle:extensions:$project.lifecycleVersion"
    annotationProcessor "android.arch.lifecycle:compiler:$project.lifecycleVersion"

    compile "com.squareup.retrofit2:retrofit:$project.retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$project.retrofitVersion"
    compile 'com.squareup.okhttp3:logging-interceptor:3.9.1'

    implementation 'com.github.bumptech.glide:glide:4.2.0'

    compile "com.jakewharton:butterknife:$project.butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$project.butterKnifeVersion"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
